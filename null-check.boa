p: Project = input;
out: output sum[string] of int;
isfixing := false;
count := 0;
files: map[string] of ChangedFile;

visit(p, visitor {
	before node: Revision -> {
        comment := node.log;
        isfixing = isfixingrevision(comment);
    }
	before node: ChangedFile -> {
		if (isfixing && haskey(files, node.name)) {
			count = 0;
			visit(getast(files[node.name]));
			last := count;
			count = 0;
			visit(getast(node));
			if (count > last){
             out[node.name] << 1;
            }

		}
		if (node.change == ChangeKind.DELETED)
			remove(files, node.name);
		else
			files[node.name] = node;
		stop;
	}
	before node: Statement ->
		if (node.kind == StatementKind.IF)
			visit(node.expression, visitor {
				before node: Expression ->
					if (node.kind == ExpressionKind.EQ || node.kind == ExpressionKind.NEQ)
						exists (i: int; isliteral(node.expressions[i], "null"))
							count++;
			});
});
